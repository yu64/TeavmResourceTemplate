
plugins {
	
	
	//TeaVM
	id 'io.github.zebalu.teavm-gradle-plugin' version '1.0.0'
	
}

eclipse.project.name = "teavm"

ext {

	teavmOutputPackage = "test/resources/";
	teavmOutput = "${project.buildDir}/resources/main";
}

dependencies {

	implementation 'org.teavm:teavm-jso-apis:0.7.0'
}


//TeaVMの設定
teavm {

	targetDirectory = new File("${teavmOutput}/${teavmOutputPackage}");
	mainClass = "test.teavm.Main";
	entryPointName = "callTeavmScript";
	targetFileName = "app.js"
}

//リソースを確定させる前に、TeaVMでリソースを生成させる
processResources.dependsOn 'teavmc'




configurations {
    teavmJar {
        canBeConsumed = true
        canBeResolved = false
        
		attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, JavaVersion.current().majorVersion.toInteger())
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, 'jar'))
		}
    }

	runtimeClasspath {
        attributes {
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, 'jar'))
        }
    }
}

//リソースだけのJarを作成するタスク
task resourceJar(type: Jar, dependsOn: processResources) {

	classifier "resources"
	from fileTree("${project.buildDir}/resources/main/")
}


//成果物を設定
artifacts {

	teavmJar resourceJar
}

//ビルドしたときに生成されるように登録
assemble.dependsOn resourceJar




